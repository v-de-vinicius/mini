plugins {
    id 'java'
    id 'application'
}

group = 'com.github.vdevinicius.mini'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
application {
    mainClass = 'com.github.vdevinicius.mini.http.Main'
}

tasks.register('fatJar', Jar) {
    archiveBaseName = "${project.name}"
    archiveClassifier = 'all'

    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath

    manifest {
        attributes(
                'Main-Class': 'com.github.vdevinicius.minihttp.Main',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    implementation 'com.google.code.gson:gson:2.11.0'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
}

test {
    useJUnitPlatform()
}